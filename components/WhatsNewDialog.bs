sub init()
    m.content = m.top.findNode("content")

    setPalette()

    m.top.id = "OKDialog"
    m.top.height = 900
    m.top.title = tr("Welcome to version") + " " + m.global.app.version
    m.top.buttons = [tr("OK")]

    dialogStyles = {
        "default": {
            "fontSize": 27,
            "fontUri": "font:SystemFontFile",
            "color": "#EFEFEFFF"
        },
        "url": {
            "fontSize": 27,
            "fontUri": "font:SystemFontFile",
            "color": "#00a4dcFF"
        }
    }

    ' empty line for spacing
    textLine = m.content.CreateChild("StdDlgMultiStyleTextItem")
    textLine.text = ""

    textLine = m.content.CreateChild("StdDlgMultiStyleTextItem")
    textLine.drawingStyles = dialogStyles
    textLine.text = tr("To view a complete list of changes visit") + " <url>https://github.com/jellyfin/jellyfin-roku/releases/tag/v" + m.global.app.version + "</url>"
end sub

sub setPalette()
    dlgPalette = createObject("roSGNode", "RSGPalette")
    dlgPalette.colors = {
        DialogBackgroundColor: "0x262828FF",
        DialogFocusColor: "0xcececeFF",
        DialogFocusItemColor: "0x202020FF",
        DialogSecondaryTextColor: "0xf8f8f8ff",
        DialogSecondaryItemColor: "#00a4dcFF",
        DialogTextColor: "0xeeeeeeFF"
    }

    m.top.palette = dlgPalette
end sub
